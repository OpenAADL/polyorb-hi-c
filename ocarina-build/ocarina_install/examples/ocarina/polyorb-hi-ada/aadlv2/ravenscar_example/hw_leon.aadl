--  $Id: hw_leon.aadl 6752 2009-07-01 16:36:45Z lasnier $

package HW_Leon
public
  with Deployment, Cheddar_Properties;
  with RavenscarExample;

processor The_Processor
features
  ETH : requires bus access Ethernet_Bus;
properties
  Deployment::Execution_Platform           => LEON_ORK;
  Cheddar_Properties::Preemptive_Scheduler => true;
  Scheduling_Protocol                      =>
    POSIX_1003_HIGHEST_PRIORITY_FIRST_PROTOCOL;
  Priority_Range                           => 0 .. 255;
end The_Processor;

bus Ethernet_Bus
properties
  Deployment::Transport_API => Spacewire;
end Ethernet_Bus;

system Case_Study
end Case_Study;

system implementation Case_Study.LEON
subcomponents
  WoM : process RavenscarExample::Workload_Manager.Impl;
  InS : process RavenscarExample::Interrupt_Simulator.Impl;
  CPU_1 : processor The_Processor;
  CPU_2 : processor The_Processor;
  B     : bus Ethernet_Bus;
connections
  bus access B -> CPU_1.ETH;
  bus access B -> CPU_2.ETH;
  port InS.External_Interrupt -> WoM.External_Interrupt_Depository
    {Actual_Connection_Binding => reference (B);};
properties
  Actual_Processor_Binding => reference (CPU_1) applies to WoM;
  Actual_Processor_Binding => reference (CPU_2) applies to InS;
end Case_Study.LEON;

end HW_Leon;
