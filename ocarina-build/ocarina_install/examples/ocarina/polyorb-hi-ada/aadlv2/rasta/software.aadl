package Software

-- This package models two threads, one pinger and one pingee. Pinger
-- pings pingee periodically.

public
  with Data_Model;

----------
-- Data --
----------

-- We take advantage of the AADL Data Modeling annex document to model
-- data types to be exchanged.

data Simple_Type
properties
  Data_Model::Data_Representation => character;
end Simple_Type;

-----------------
-- Subprograms --
-----------------

subprogram Do_Ping_Spg
features
  Data_Source : out parameter Simple_Type;
properties
  source_language => Ada95;
  source_name     => "Ping.Do_Ping_Spg";
end Do_Ping_Spg;

subprogram Ping_Spg
features
  Data_Sink : in parameter Simple_Type;
properties
  source_language => Ada95;
  source_name     => "Pinged.Ping_Spg";
end Ping_Spg;

-------------
-- Threads --
-------------

thread Pinger
features
  Data_Source : out event data port Simple_Type;
end Pinger;

thread implementation Pinger.Impl
calls 
Mycall : {
  P_Spg : subprogram Do_Ping_Spg;
};
connections
  parameter P_Spg.Data_Source -> Data_Source;
properties
  Source_Stack_Size => 512 KByte;
  Dispatch_Protocol                  => Periodic;
  Period                             => 3 sec;
  Priority                           => 2;
end Pinger.Impl;

thread Pinger2
features
  Data_Source : out event data port Simple_Type;
end Pinger2;

thread implementation Pinger2.Impl
calls 
Mycall : {
  P_Spg : subprogram Do_Ping_Spg;
};
connections
  parameter P_Spg.Data_Source -> Data_Source;
properties
  Dispatch_Protocol                  => Periodic;
  Period                             => 10 sec;
  Priority                           => 3;
end Pinger2.Impl;

thread Pingee
features
  Data_Sink : in event data port Simple_Type;
end Pingee;

thread implementation Pingee.Impl
calls 
Mycall : {
  Q_Spg : subprogram Ping_Spg;
};
connections
  parameter Data_Sink -> Q_Spg.Data_Sink;
properties
  Dispatch_Protocol                  => Sporadic;
  Period                             => 10 ms;
  Deadline                           => 10 ms;
  Priority                           => 5;
end Pingee.Impl;

thread Pingee2
features
  Data_Sink : in event data port Simple_Type;
end Pingee2;

thread implementation Pingee2.Impl
calls 
Mycall : {
  Q_Spg : subprogram Ping_Spg;
};
connections
  parameter Data_Sink -> Q_Spg.Data_Sink;
properties
  Dispatch_Protocol                  => Sporadic;
  Period                             => 10 ms;
  Deadline                           => 10 ms;
  Priority                           => 6;
end Pingee2.Impl;

end Software;
