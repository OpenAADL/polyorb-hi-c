--  08 20 2008
--  Olivier GILLES

package s
public
  with Data_Model;
  with Cheddar_Properties;
  with transformations;

----------
-- Data --
----------

data Simple_Type
properties
  Data_Model::Data_Representation  => integer;
end Simple_Type;

-----------------
-- Subprograms --
-----------------

subprogram Do_Ping_Spg
features
  Data_Source : out parameter Simple_Type;
end Do_Ping_Spg;

subprogram implementation Do_Ping_Spg.Impl
properties
  source_language => Ada95;
  source_name     => "Ping.Do_Ping_Spg";
end Do_Ping_Spg.Impl;

subprogram Ping_Spg
features
  Data_Sink : in parameter Simple_Type;
properties
  source_language => Ada95;
  source_name     => "Pinged.Ping_Spg";
end Ping_Spg;

-------------
-- Threads --
-------------

thread P
features
  Data_Source : out event data port Simple_Type;
end P;

thread implementation P.Impl
calls CS1 : {
  P_Spg : subprogram Do_Ping_Spg.Impl;
};
connections
  parameter P_Spg.Data_Source -> Data_Source;
properties
  Dispatch_Protocol                  => Periodic;
  Period                             => 600 Ms;
  Compute_Execution_time             => 0 ms .. 2 ms;
  Deadline                           => 1000 ms;
  Cheddar_Properties::Fixed_Priority => 4;
end P.Impl;

thread Q
features
  Data_Sink : in event data port Simple_Type;
  Data_Source : out event data port Simple_Type;
end Q;

thread implementation Q.Impl
calls CS1 : {
  Q_Spg : subprogram Ping_Spg;
};
CS2 : {
  Q2_Spg : subprogram Do_Ping_Spg.Impl;
};
connections
  parameter Data_Sink -> Q_Spg.Data_Sink;
  parameter Q2_Spg.Data_Source -> Data_Source;
properties
  compute_entrypoint_call_sequence => reference ( CS2 );
  compute_entrypoint_call_sequence => reference ( CS1 )
      applies to data_sink;
  Dispatch_Protocol                  => hybrid;
  Cheddar_Properties::Fixed_Priority => 2;
  Period                             => 1000 Ms;
end Q.Impl;

end s;