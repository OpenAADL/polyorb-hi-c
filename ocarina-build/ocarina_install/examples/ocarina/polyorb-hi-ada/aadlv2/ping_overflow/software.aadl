--  This AADL model illustrates how to model a simple interaction
--  between tasks, in a local or distributed environment.
--
--  Two task pings another, sending a data periodically.
--
--  $Id: software.aadl 6936 2009-08-18 15:33:06Z lanarre $

package Software
public
  with Data_Model;

----------
-- Data --
----------

data Simple_Type
properties
  Data_Model::Data_Representation => integer;
end Simple_Type;

data Opaque_Type
properties
  Source_Language => Ada95;
  Type_Source_Name => "User_Types.The_Opaque_Type";
end Opaque_Type;

-----------------
-- Subprograms --
-----------------

subprogram Do_Ping_Spg
features
  Data_Source : out parameter Opaque_Type;
end Do_Ping_Spg;

subprogram Do_Convert_Spg
features
  Data_Sink : in parameter Opaque_Type;
  Data_Source : out parameter Simple_Type;
end Do_Convert_Spg;

subprogram implementation Do_Ping_Spg.Impl
properties
  source_language => Ada95;
  source_name     => "Ping.Do_Ping_Spg";
end Do_Ping_Spg.Impl;

subprogram implementation Do_Convert_Spg.Impl
properties
  source_language => Ada95;
  source_name     => "Ping.Do_Convert_Spg";
end Do_Convert_Spg.Impl;

subprogram implementation Do_Convert_Spg.Impl2
properties
  source_language => Ada95;
  source_name     => "Ping.Do_Convert_Spg2";
end Do_Convert_Spg.Impl2;

-------------
-- Threads --
-------------

subprogram P_Wrapper
features
  Data_Source : out parameter Simple_Type;
end P_Wrapper;

subprogram implementation P_Wrapper.Impl
calls 
Mycalls : {
  P_Spg : subprogram Do_Ping_Spg.Impl;
  C_Spg : subprogram Do_Convert_Spg.Impl;
};
connections
  parameter P_Spg.Data_Source -> C_Spg.Data_Sink;
  parameter C_Spg.Data_Source -> Data_Source;
end P_Wrapper.Impl;

subprogram implementation P_Wrapper.Impl2
calls 
Mycalls : {
  P_Spg : subprogram Do_Ping_Spg.Impl;
  C_Spg : subprogram Do_Convert_Spg.Impl2;
};
connections
  parameter P_Spg.Data_Source -> C_Spg.Data_Sink;
  parameter C_Spg.Data_Source -> Data_Source;
end P_Wrapper.Impl2;

thread P
features
  Data_Source : out event data port Simple_Type;
end P;

thread implementation P.Impl
calls 
Mycall : {
  P_Spg : subprogram P_Wrapper.Impl;
};
connections
  parameter P_Spg.Data_Source -> Data_Source;
properties
  Initialize_Entrypoint_Source_Text  => "Msgs.Welcome_Pinger";
  Recover_Entrypoint_Source_Text     => "Msgs.Recover";
  Dispatch_Protocol                  => Periodic;
  Period                             => 300 Ms;
  Compute_Execution_time             => 0 ms .. 3 ms;
  Deadline                           => 300 ms;
  Priority                           => 2;
end P.Impl;

thread implementation P.Impl2
calls 
Mycall : {
  P_Spg : subprogram P_Wrapper.Impl2;
};
connections
  parameter P_Spg.Data_Source -> Data_Source;
properties
  Initialize_Entrypoint_Source_Text  => "Msgs.Welcome_Pinger";
  Recover_Entrypoint_Source_Text     => "Msgs.Recover";
  Dispatch_Protocol                  => Periodic;
  Period                             => 1000 Ms;
  Compute_Execution_time             => 0 ms .. 3 ms;
  Deadline                           => 1000 ms;
  Priority                           => 2;
end P.Impl2;

thread Q
features
  Data_Sink : in event data port Simple_Type
   {Compute_Entrypoint_Source_Text => "Pinged.Ping_Spg";
    Urgency            => 1;};
  Data_Sink2 : in event data port Simple_Type
   {Compute_Entrypoint_Source_Text => "Pinged.Ping_Spg";
    Urgency            => 2;};
end Q;

thread implementation Q.Impl
properties
  Dispatch_Protocol                  => Sporadic;
  Period                             => 1000 Ms;
  deadline                           => 1000 Ms;
  Compute_Execution_time             => 0 ms .. 3 ms;
  Priority                           => 1;
end Q.Impl;

end Software;
