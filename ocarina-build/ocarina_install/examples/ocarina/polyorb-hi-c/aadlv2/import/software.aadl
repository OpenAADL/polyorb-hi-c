--  This AADL model illustrates how to model a simple interaction
--  between tasks, in a local or distributed environment.
--
--  One task pings another, sending a data periodically.
--
--  $Id: software.aadl 6898 2009-07-27 11:53:15Z lasnier $

package Software
public
  with Data_Model, Cheddar_Properties;

----------
-- Data --
----------

data Simple_Type
properties
  Data_Model::Data_Representation => integer;
end Simple_Type;

-----------------
-- Subprograms --
-----------------

subprogram Do_Ping_Spg
features
  Data_Source : out parameter Simple_Type;
properties
  source_language => Ada95;
  source_name     => "Ping.Do_Ping_Spg";
  source_text => ("ping.adb");
end Do_Ping_Spg;

subprogram Hello
properties
  source_language => CPP;
  source_name     => "hello";
  source_text     => ("import_cpp.cpp");
end Hello;

subprogram Ping_Spg
features
  Data_Sink : in parameter Simple_Type;
properties
  source_language => C;
  source_name     => "ping_spg";
  source_text     => ("import.c");
end Ping_Spg;

subprogram Ping_Wrapper
features
  Data_Sink : in parameter Simple_Type;
end Ping_Wrapper;

subprogram implementation Ping_Wrapper.Impl
calls
Mycalls: {
  CPP_Spg : subprogram Hello;
  P_Spg : subprogram Ping_Spg;
};
connections
  parameter Data_Sink -> P_Spg.Data_Sink;
end Ping_Wrapper.Impl;

-------------
-- Threads --
-------------

thread P
features
  Data_Source : out event data port Simple_Type;
end P;

thread implementation P.Impl
calls
Mycall : {
  P_Spg : subprogram Do_Ping_Spg;
};
connections
  parameter P_Spg.Data_Source -> Data_Source;
properties
  Dispatch_Protocol                  => Periodic;
  Period                             => 1000 Ms;
  Compute_Execution_time             => 0 ms .. 3 ms;
  Deadline                           => 1000 ms;
  Cheddar_Properties::Fixed_Priority => 2;
end P.Impl;

thread Q
features
  Data_Sink : in event data port Simple_Type;
end Q;

thread implementation Q.Impl
calls
Mycall : {
  Q_Spg : subprogram Ping_Wrapper.impl;
};
connections
  parameter Data_Sink -> Q_Spg.Data_Sink;
properties
  Dispatch_Protocol                  => Sporadic;
  Period                             => 10 Ms;
  deadline                           => 10 Ms;
  Compute_Execution_time             => 0 ms .. 3 ms;
  Cheddar_Properties::Fixed_Priority => 1;
end Q.Impl;

end Software;
